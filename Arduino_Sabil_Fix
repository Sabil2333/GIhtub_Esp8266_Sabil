#include <Servo.h>
#include <DHT.h>
#include <LiquidCrystal_I2C.h>

#define TANAH A0      
#define SUHU 2        
#define SERVO 9      
#define LED 13  
#define BUZZER 8      

DHT dht(SUHU, DHT22);
Servo valve;
LiquidCrystal_I2C lcd(0x27, 16, 2);

int KERING = 300;    
int LAMA_SIRAM = 5000;
unsigned long waktu_siram = 0;
bool sedang_siram = false;

void setup() {
  Serial.begin(9600);
  Serial.println("Sistem Siram Otomatis");
  
  pinMode(LED, OUTPUT);
  pinMode(BUZZER, OUTPUT);
  
  dht.begin();
  valve.attach(SERVO);
  valve.write(0);
  
  // Inisialisasi LCD
  lcd.init();
  lcd.backlight();
  lcd.print("SISTEM SIRAM");
  lcd.setCursor(0, 1);
  lcd.print("OTOMATIS");
  delay(2000);
  lcd.clear();
  
  // Bunyi startup
  tone(BUZZER, 1000, 200);
  delay(1000);
}

void loop() {
  int nilai_tanah = analogRead(TANAH);
  int persen = map(nilai_tanah, 0, 1023, 100, 0);
  
  // Baca DHT sensor
  float suhu = dht.readTemperature();
  float kelembaban = dht.readHumidity();
  
  // Tampilkan status di Serial
  Serial.print("Tanah: ");
  Serial.print(persen);
  Serial.print("% | Suhu: ");
  Serial.print(suhu);
  Serial.print("Â°C | Udara: ");
  Serial.print(kelembaban);
  Serial.print("% - ");
  
  // Cek kondisi tanah
  if (nilai_tanah < KERING && !sedang_siram) {
    mulai_siram();
  } else if (sedang_siram && (millis() - waktu_siram >= LAMA_SIRAM)) {
    stop_siram();
  }
  
  // Status
  if (sedang_siram) {
    Serial.println("SIRAM");
    digitalWrite(LED, HIGH);
  } else {
    Serial.println(nilai_tanah < KERING ? "KERING" : "NORMAL");
    digitalWrite(LED, LOW);
  }
  
  // Update LCD dengan tampilan bergantian
  static bool tampil_dht = false;
  lcd.clear();
  
  if (tampil_dht) {
    // Tampilkan data DHT
    lcd.setCursor(0, 0);
    lcd.print("Suhu: ");
    if (!isnan(suhu)) {
      lcd.print(suhu, 1);
      lcd.print("C");
    } else {
      lcd.print("Error");
    }
    
    lcd.setCursor(0, 1);
    lcd.print("Udara: ");
    if (!isnan(kelembaban)) {
      lcd.print(kelembaban, 1);
      lcd.print("%");
    } else {
      lcd.print("Error");
    }
  } else {
    // Tampilkan data tanah dan status
    lcd.setCursor(0, 0);
    lcd.print("Tanah: ");
    lcd.print(persen);
    lcd.print("%");
    
    lcd.setCursor(0, 1);
    if (sedang_siram) {
      lcd.print("Status: SIRAM");
    } else if (nilai_tanah < KERING) {
      lcd.print("Status: KERING");
    } else {
      lcd.print("Status: NORMAL");
    }
  }
  
  tampil_dht = !tampil_dht; // Toggle tampilan
  delay(3000);
}

void mulai_siram() {
  Serial.println(">> MULAI SIRAM <<");
  sedang_siram = true;
  waktu_siram = millis();
  valve.write(90);
  tone(BUZZER, 800, 500);
}

void stop_siram() {
  Serial.println(">> SELESAI SIRAM <<");
  sedang_siram = false;
  valve.write(0);
  tone(BUZZER, 1200, 300);
}
